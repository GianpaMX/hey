apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply from: 'jacoco.gradle'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    defaultConfig {
        applicationId "net.ddns.softux.hey"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {
            minifyEnabled false
            testCoverageEnabled true
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '23.4.0'
    final ESPRESSO_VERSION = '2.2.1'
    final JUNIT_VERSION = '4.12'
    final RUNNER_VERSION = '0.4'
    final DAGGER_VERSION = '2.2'
    final DAGGER_MOCK_VERSION = '0.6'
    final DEXMAKER_VERSION = '1.4'
    final MOCKITO_VERSION = '1.10.19'
    final HAMCREST_VERSION = '1.3'

    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    testApt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    androidTestApt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    compile 'com.firebase:firebase-client-android:2.5.2+'
    compile "com.google.firebase:firebase-database:9.0.2"
    compile 'com.firebaseui:firebase-ui:0.4.0'


    testCompile "junit:junit:$JUNIT_VERSION"
    testCompile "org.hamcrest:hamcrest-core:$HAMCREST_VERSION"
    testCompile "org.hamcrest:hamcrest-library:$HAMCREST_VERSION"
    testCompile "org.hamcrest:hamcrest-integration:$HAMCREST_VERSION"
    testCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
    testCompile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    testCompile "com.github.fabioCollini:DaggerMock:$DAGGER_MOCK_VERSION"
    testCompile "org.robolectric:robolectric:3.0"
    testCompile('org.robolectric:shadows-support-v4:3.0') {
        exclude group: 'com.android.support', module: 'support-v4'
    }
    testCompile "com.squareup.assertj:assertj-android-recyclerview-v7:1.1.1@aar"

    androidTestCompile "junit:junit:$JUNIT_VERSION"
    androidTestCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
    androidTestCompile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$ESPRESSO_VERSION") {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestCompile "com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$ESPRESSO_VERSION"
    androidTestCompile "com.android.support.test:runner:$RUNNER_VERSION"
    androidTestCompile "com.android.support.test:rules:$RUNNER_VERSION"
    androidTestCompile "com.github.fabioCollini:DaggerMock:$DAGGER_MOCK_VERSION"
    androidTestCompile "com.crittercism.dexmaker:dexmaker:$DEXMAKER_VERSION"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:$DEXMAKER_VERSION"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:$DEXMAKER_VERSION"

}

apply plugin: 'com.google.gms.google-services'
